<h2>Run HelloWorld using Gradle</h2>

<p>
    Similar to Maven, we can declare JavaFX jars as dependency in <kbd>build.gradle</kbd>.
    However, for Gradle we need to find and specify the platform/OS as classifier. This requires a
    small script:
</p>

<pre><code>
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
    }
}

apply plugin: 'application'
apply plugin: 'com.google.osdetector'

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os
</code></pre>

<p>Next, we add all the dependencies using the platform:</p>

<pre><code>
dependencies {
    compile "org.openjfx:javafx-base:11:$platform"
    compile "org.openjfx:javafx-graphics:11:$platform"
    compile "org.openjfx:javafx-controls:11:$platform"
}
</code></pre>

<p>
    Please note that classifiers are not taken into account when resolving
    transitive dependencies in Gradle. Therefore, we need to specify all three JavaFX modules
    with platform as classifier.
</p>

<p>
    Next, we set <kbd>--module-path</kbd> to the value that would have been the classpath
    and add <kbd>javafx.controls</kbd> as a module to both <kbd>javac</kbd> and <kbd>java</kbd> commands.
    This makes sense because all the dependencies are currently on the classpath.
</p>

<pre><code>
compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls'
        ]
    }
}

run {
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'javafx.controls'
        ]
    }
}
</code></pre>

<p>
    Here is a <a href="build.gradle">build.gradle</a> file which shows how to achieve this.
</p>
<p>
    Run the application (e.g. use <a href="HelloFX.java">HelloFX.java</a>) using:
</p>

<pre><code>
gradle run
</code></pre>

<div class="alert alert-warning">
    <strong>Note: </strong>
    Make sure to use gradle version 4.10+ and set the JAVA_HOME environment variable to JDK 11.
</div>
